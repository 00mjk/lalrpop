use expr_arena_ast::{Arena, Node, Op};
use util::tok::Tok;

grammar<'ast>(arena: &'ast Arena<'ast>);

extern {
    type Location = usize;

    enum Tok {
        "(" => Tok::LParen(..),
        ")" => Tok::RParen(..),
        "-" => Tok::Minus(..),
        "+" => Tok::Plus(..),
        "*" => Tok::Times(..),
        "/" => Tok::Div(..),
        "," => Tok::Comma(..),
        Num => Tok::Num(<i32>)
    }
}

pub Expr: &'ast Node<'ast> = {
    <l:Expr> "-" <r:Factor> => arena.alloc(Node::Binary(Op::Sub, l, r));
    <l:Expr> "+" <r:Factor> => arena.alloc(Node::Binary(Op::Add, l, r));
    Factor;
};

Comma<T>: Vec<T> = {
    <h:(<T> ",")*> <t:T?> =>
        h.into_iter().chain(t).collect();
};

Factor = {
    <l:Factor> "*" <r:Term> => arena.alloc(Node::Binary(Op::Mul, l, r));
    <l:Factor> "/" <r:Term> => arena.alloc(Node::Binary(Op::Div, l, r));
    "*" "(" <Comma<Expr>> ")" => arena.alloc(Node::Reduce(Op::Mul, <>));
    Term;
};

Term = {
    <n:Num> => arena.alloc(Node::Value(n));
    "(" <Expr> ")";
};
